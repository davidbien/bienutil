if(NOT EXISTS "@CMAKE_CURRENT_BINARY_DIR@/install_manifest.txt")
    message(FATAL_ERROR "Cannot find install manifest: @CMAKE_CURRENT_BINARY_DIR@/install_manifest.txt")
endif()

file(READ "@CMAKE_CURRENT_BINARY_DIR@/install_manifest.txt" files)
string(REGEX REPLACE "\n" ";" files "${files}")
foreach(file ${files})
    message(STATUS "Uninstalling \"$ENV{DESTDIR}${file}\"")
    if(EXISTS "$ENV{DESTDIR}${file}")
        execute_process(
            COMMAND @CMAKE_COMMAND@ -E remove "$ENV{DESTDIR}${file}"
            OUTPUT_VARIABLE rm_out
            RESULT_VARIABLE rm_retval
            )
        if(NOT "${rm_retval}" EQUAL 0)
            message(FATAL_ERROR "Problem when removing \"$ENV{DESTDIR}${file}\"")
        endif()
    else()
        message(STATUS "File \"$ENV{DESTDIR}${file}\" does not exist.")
    endif()
endforeach(file)

# Remove directories
foreach(dir @MOD_UNINSTALL_DIRECTORIES@)
    message(STATUS "Removing directory \"$ENV{DESTDIR}${dir}\"")
    if(IS_DIRECTORY "$ENV{DESTDIR}${dir}")
        execute_process(
            COMMAND @CMAKE_COMMAND@ -E remove_directory "$ENV{DESTDIR}${dir}"
            OUTPUT_VARIABLE rm_out
            RESULT_VARIABLE rm_retval
            )
        if(NOT "${rm_retval}" EQUAL 0)
            message(FATAL_ERROR "Problem when removing \"$ENV{DESTDIR}${dir}\"")
        endif()
    else()
        message(STATUS "Directory \"$ENV{DESTDIR}${dir}\" does not exist.")
    endif()
endforeach(dir)